REM * ROCK, PAPER, SCISSORS
REM * BASED ON 'ROCKSP' FROM
REM * 101 BASIC COMPUTER GAMES

1 CALL CLEAR
2 PRINT "** ROCK, PAPER, SCISSORS **"
3 INPUT "HOW MANY GAMES? ":Q

10 FOR G=1 TO Q
11 RANDOMIZE
14 CALL CLEAR
15 PRINT "GAME #";G;" OF ";Q
16 PRINT

20 PRINT "1: ROCK"
21 PRINT "2: PAPER" 
22 PRINT "3: SCISSORS"
23 GOTO 700
24 INPUT "YOUR CHOICE => ":K
25 IF (K-1)*(K-2)*(K-3)=0 THEN 29
26 PRINT "** ENTER 1, 2, OR 3 **"
27 GOTO 24
29 CALL CLEAR

30 ON K GOTO 31,33,35
31 K=3
32 GOTO 40
33 K=1
34 GOTO 40
35 K=2

40 PRINT "YOU CHOSE........";
41 ON K GOTO 44,46,42
42 PRINT "ROCK"
43 GOTO 50
44 PRINT "PAPER"
45 GOTO 50
46 PRINT "SCISSORS"

50 X=INT(RND*3+1)
51 PRINT "COMPUTER CHOSE...";
52 ON X GOTO 53,55,57
53 PRINT "PAPER"
54 GOTO 60
55 PRINT "SCISSORS"
56 GOTO 60
57 PRINT "ROCK"
59 PRINT

60 IF X=K THEN 70
61 IF X>K THEN 80
62 IF X=1 THEN 90
63 PRINT "** YOU WIN **"
64 H=H+1
69 GOTO 880

70 PRINT "** TIE, NO WINNER **"
71 GOTO 880

80 IF K<>1 THEN 82 
81 IF X<>3 THEN 82 ELSE 63
82 PRINT "** COMPUTER WINS **"
83 C=C+1
89 GOTO 880

90 IF K=3 THEN 82 ELSE 63

700 PRINT
701 IF G=1 THEN 24
702 IF H>C THEN 720
703 IF C>H THEN 730
710 PRINT "* YOU ARE TIED *"
711 PRINT "(";C;"-";H;")"
719 GOTO 798
720 PRINT "* YOU ARE IN THE LEAD *"
721 PRINT "(";H;"-";C;")"
729 GOTO 798
730 PRINT "* COMPUTER IS IN THE LEAD *"
731 PRINT "(";C;"-";H;")"

798 PRINT
799 GOTO 24

880 PRINT
881 PRINT "** ANY KEY TO CONTINUE **"
882 CALL KEY(0,P,K)
883 IF K=0 THEN 882
888 NEXT G

990 CALL CLEAR
991 PRINT "** SESSION COMPLETE **"
992 PRINT "GAMES................";Q
993 PRINT "YOUR VICTORIES.......";H
994 PRINT "COMPUTER VICTORIES...";C
995 PRINT "TIES.................";Q-(C+H)
996 PRINT
997 PRINT "FINAL SCORE..........";H-C
998 PRINT
999 END

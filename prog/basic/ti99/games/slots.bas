REM * SLOT MACHINE
REM * BASED ON SLOTS
REM * FROM '101 BASIC COMPUTER GAMES'

1 CALL CLEAR
2 DIM D(3)
3 INPUT "BANKROLL: $ ":B
4 IF B<1 THEN 3
5 I=B
9 PRINT

20 FOR B1=1 TO 3
21 RANDOMIZE
22 LET D(B1)=INT(RND*6+1)
23 NEXT B1

30 FOR G1=1 TO 3
31 RANDOMIZE
32 ON D(G1) GOTO 40,42,44,46,48,50

40 PRINT "BELL ";
41 GOTO 59
42 PRINT "BAR ";
43 GOTO 59
44 PRINT "CHERRY ";
45 GOTO 59
46 PRINT "APPLE ";
47 GOTO 59
48 PRINT "LEMON ";
49 GOTO 59
50 PRINT "$$$ ";
59 NEXT G1

60 PRINT
61 PRINT
62 IF D(1)<>D(2) THEN 65
63 IF D(2)=D(3) THEN 100
64 IF D(1)=D(2) THEN 67
65 IF D(1)<>D(3) THEN 94
66 GOTO 97
67 IF D(1)/2<>INT(D(1)/2) THEN 97

90 B=B+5
91 PRINT "** KENO **"
92 PRINT "WON:   $ 5"
93 GOTO 108
94 B=B-1
95 PRINT "LOST:  $ 1"
96 GOTO 108
97 B=B+1
98 PRINT "WON:   $ 1"
99 GOTO 108
100 B=B+20
101 PRINT "** JACKPOT **"
102 PRINT "WON:   $ 20"
108 PRINT "TOTAL: $";B
109 GOTO 900

900 IF B<1 THEN 990
910 CALL KEY(0,A,K)
911 IF K=0 THEN 910
912 IF A=81 THEN 995
918 PRINT
919 GOTO 20

990 PRINT "** OUT OF MONEY **"
991 GOTO 999
995 PRINT
997 PRINT "FINAL BALANCE... $";B
998 PRINT "RETURN..........";((B/I)*100)-100;"%"
999 END
